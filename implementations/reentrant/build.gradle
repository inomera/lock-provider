plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    api project(':lock-provider-api')

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.2.0')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.2.0')
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation('org.mockito:mockito-junit-jupiter:2.18.3')
    testImplementation('org.hamcrest:hamcrest-all:1.3')
    testImplementation('com.google.guava:guava:33.3.1-jre')
}

apply from: "${rootProject.rootDir}/gradle/tests.gradle"

def isSnapshot = Boolean.parseBoolean(System.getenv("SNAPSHOT_RELEASE"))
def mavenSnapshotUrl = project.findProperty('mavenSnapshotUrl') ?: 'https://oss.sonatype.org/content/repositories/snapshots/'
def mavenReleaseUrl = project.findProperty('mavenReleaseUrl') ?: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
def mavenUsername = project.findProperty('mavenUsername') ?: 'Undefined'
def mavenPassword = project.findProperty('mavenPassword') ?: 'Undefined'
def mavenPackageGroup = project.findProperty('mavenPackageGroup') ?: 'com.inomera'

group = 'com.inomera.telco.commons'
version = project.file('VERSION').text.trim()
sourceCompatibility = JavaVersion.VERSION_23
targetCompatibility = JavaVersion.VERSION_23

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testImplementation {
        extendsFrom compileOnly
    }
}

compileJava.dependsOn(processResources)

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

artifacts {
    archives sourceJar
    archives javadocJar
}

// Signing
signing {
    sign configurations.archives
}
signArchives.onlyIf { gradle.taskGraph.hasTask(publish) && !isSnapshot }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            pom {
                name = 'lock-provider'
                description = 'Local Reentrant locks made easy'
                url = 'https://github.com/inomera/lock-provider'

                organization {
                    name = 'Inomera Research'
                    url = 'https://inomera.com/'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'sedran'
                        name = 'Serdar Kuzucu'
                        organization = 'Inomera'
                        organizationUrl = 'https://github.com/sedran'
                    }
                    developer {
                        id = 'turgaycan'
                        name = 'Turgay Can'
                        organization = 'Inomera'
                        organizationUrl = 'https://github.com/turgaycan'
                    }
                    developer {
                        id = 'ebruuzorluu'
                        name = 'Ebru Zorlu'
                        organization = 'Inomera'
                        organizationUrl = 'https://github.com/ebruuzorluu'
                    }
                }
                scm {
                    url = 'https://github.com/inomera/lock-provider'
                    connection = 'scm:git:https://github.com/inomera/lock-provider.git'
                    developerConnection = 'scm:git:https://github.com/inomera/lock-provider.git'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/inomera/lock-provider/issues'
                }
            }

            // create the sign pom artifact
            pom.withXml {
                def root = asNode()

                // eliminate test-scoped dependencies (no need in maven central POMs)
                root.dependencies.removeAll { dep ->
                    dep.scope == "test"
                }

                def pomFile = file("${project.buildDir}/${project.name}-${project.version}.pom")
                writeTo(pomFile)
                println("pomFile -> $pomFile")
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                println("pomAscFile -> $pomAscFile")
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts

            def archives = project.tasks.signArchives
            println("archives -> $archives")

            def files = archives.signatureFiles
            println("signatureFiles -> $files")
            files.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    println("it.file -> $it.file")
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                        println("classifier -> $classifier")
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }

    repositories {
        maven {
            url = isSnapshot ? "${mavenSnapshotUrl}" : "${mavenReleaseUrl}"
            println("Publishing to ${url}")
            credentials {
                username "${mavenUsername}"
                println("username -> ${mavenUsername}")
                password "${mavenPassword}"
                println("password -> ${mavenPassword}")
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$projectDir/${project.name}-${project.version}.pom")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

tasks.generatePomFileForMavenJavaPublication {
    destination = file("${project.projectDir}/build/publications/mavenJava/${project.name}-${project.version}.pom")
}

